plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'

android {

    compileSdkVersion rootProject.ext.sdkVersion

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.ext.sdkVersion
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    exoPlayerVersion = "2.8.3"
    libraryVersion = '1.0.0'
    publishedGroupId = 'com.redmadrobot.gallery'
    artifact = 'android-gallery'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")
    pkg {
        repo = 'android-maven'
        name = 'android-galery'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/redmadrobot-spb/android-gallery.git'

        version {
            name = libraryVersion
            vcsTag = libraryVersion
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

publishing {
    publications {
        publishAndroidGallery(MavenPublication) {
            groupId publishedGroupId
            artifactId artifact
            version libraryVersion
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/gallery-release.aar")
        }
    }
}

dependencies {

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Androidx
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'androidx.core:core-ktx:1.0.1'
    // Photo view
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    // Video player
    implementation "com.google.android.exoplayer:exoplayer-core:$exoPlayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoPlayerVersion"
    // Image loader
    implementation 'com.github.bumptech.glide:glide:4.7.1'
}
